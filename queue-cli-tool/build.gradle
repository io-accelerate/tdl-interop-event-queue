

dependencies {
    compile project(':queue-client')
    compile 'org.apache.commons:commons-csv:1.5'

    compile 'org.reflections:reflections:0.9.11'
    compile 'com.typesafe:config:1.3.1'
    compile 'com.google.guava:guava:23.6-jre'
}

// Making the app runnable

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'tdl.participant.queue.clitool.QueueCLITool'

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

//~~~~~ Release management

/**
 * Prerequisites
 *    - Create package in Bintray
 *
 * Now you can use regular maven upload to push files to you package.
 * The version will be created automatically
 *
 * To push to Maven Central use Bintray sync features:
 *    - Created request for jcenter ( if missing )
 *    - Manually sync the two repos
 *
 * To publish new version use the Bintray UI:
 *    - publish current artifacts
 *    - sync with Maven Central
 */
apply plugin: 'maven'

private static String readSystem(String property) {
    System.getenv(property) != null  ? System.getenv(property) : "unset"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'bintray-julianghionoiu-queue-cli-tool'
        repository(id: 'bintray-julianghionoiu-queue-cli-tool',
                url: 'https://api.bintray.com/maven/julianghionoiu/maven/queue-cli-tool') {
            authentication(userName: readSystem("BINTRAY_USER"), password: readSystem("BINTRAY_KEY"))
        }

        pom.project {
            name 'CLI tool for the event queue'
            description 'CLI tool to place message on the queue'
            url 'https://github.com/julianghionoiu/tdl-interop-event-queue'

            developers {
                developer {
                    name 'Iulian (Julian) Ghionoiu'
                    email 'iulian.ghionoiu@gmail.com'
                    organization 'Iulian Ghionoiu'
                    organizationUrl 'https://github.com/julianghionoiu'
                }
            }

            scm {
                connection 'scm:git:git@github.com:julianghionoiu/tdl-interop-event-queue.git'
                developerConnection 'scm:git:git@github.com:julianghionoiu/tdl-interop-event-queue.git'
                url 'git@github.com:julianghionoiu/tdl-interop-event-queue.git'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}
