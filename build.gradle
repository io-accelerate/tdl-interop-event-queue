buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.13"
        classpath "ch.netzwerg:gradle-release-plugin:1.2.4"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "net.ltgt.apt"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    group = 'ro.ghionoiu'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'ch.qos.logback:logback-classic:1.1.3'
        compile 'org.slf4j:log4j-over-slf4j:1.7.25'

        compileOnly 'org.projectlombok:lombok:1.16.20'
        apt "org.projectlombok:lombok:1.16.20"

        testCompile ('junit:junit:4.12') {
            exclude module: 'hamcrest-core'
        }
        testCompile ('org.mockito:mockito-core:1.10.19') {
            exclude module: 'hamcrest-core'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'net.trajano.commons:commons-testing:1.0.1'
        testCompile 'nl.jqno.equalsverifier:equalsverifier:1.5'
        testCompile 'org.mutabilitydetector:MutabilityDetector:0.9.2'
    }

    //Separate test results. http://stackoverflow.com/questions/12675080/how-to-cleanly-separate-two-instances-of-the-test-task-in-a-gradle-build/19251645#19251645
    //noinspection GroovyAssignabilityCheck
    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }
}

/**
 * Apply the release script to increment version numbers
 */
apply plugin: "ch.netzwerg.release"

release {
    dependsOn subprojects.build // 'distZip' could e.g be used in combination with the 'application' plugin
    push = false // 'true' would e.g. be useful when triggering the release task on a CI server
    versionSuffix = '-SNAPSHOT' // '.DEV' or '' (empty) could be useful alternatives
    tagPrefix = 'v' // 'r' or '' (empty) could be useful alternatives
}