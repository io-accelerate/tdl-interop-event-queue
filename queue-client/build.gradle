plugins {
    // Ref here: https://docs.gradle.org/8.6/userguide/publishing_setup.html
    id('java-library')
    id('signing')
    id('maven-publish')
    id('idea')
}

apply from: project(':build-logic').file('shared.gradle')
apply from: project(':build-logic').file('acceptance.gradle')

ext {
    jacksonVersion = '2.8.4'
}

dependencies {
    implementation('com.amazonaws:aws-java-sdk-sqs:1.11.253')

    implementation('com.fasterxml.jackson.core:jackson-core:'+jacksonVersion)
    implementation('com.fasterxml.jackson.core:jackson-databind:'+jacksonVersion)
    implementation('com.fasterxml.jackson.core:jackson-annotations:'+jacksonVersion)
}



// Refs:
// https://stackoverflow.com/a/68505768
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = 'Java client for TDL system'
                description = 'A Java client that allows users to get up to speed with the TDL system.'
                url = 'https://github.com/julianghionoiu/tdl-interop-event-queue'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Iulian (Julian) Ghionoiu'
                        email = 'iulian.ghionoiu@gmail.com'
                        organization = 'Iulian Ghionoiu'
                        organizationUrl = 'https://github.com/julianghionoiu'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:julianghionoiu/tdl-interop-event-queue.git'
                    developerConnection = 'scm:git:git@github.com:julianghionoiu/tdl-interop-event-queue.git'
                    url = 'git@github.com:julianghionoiu/tdl-interop-event-queue.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty('mavenCentralUsername')
                password = project.findProperty('mavenCentralPassword')
            }
        }
    }
}

// Signs using gpg-agent
// Key name configured under "~/.gradle/gradle.properties"
//
// Refs:
// https://docs.gradle.org/7.4.1/userguide/signing_plugin.html#sec:using_gpg_agent
signing {
    useGpgCmd()
    sign publishing.publications.release
}
